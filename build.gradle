buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.codehaus.groovy:groovy-all:2.3.6'
        classpath 'nu.studer:gradle-plugindev-plugin:1.0.3'
    }
}

apply plugin: 'cpp'
apply plugin: 'objective-c'
apply plugin: 'groovy'
apply plugin: 'nu.studer.plugindev'

group = 'com.github.cr0'

// find osx sdk
def osxSDKRoot = new File("/Applications/Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/")
if (!osxSDKRoot.exists()) throw new GradleException("SDK root does not exist")

def sdk = null
for (def c : osxSDKRoot.listFiles()) {
    if (c.name.compareTo("MacOSX10.8.sdk") < 0) continue // fancy ;)

    if (sdk == null) sdk = c.name
    else if (sdk.compareTo(c.name) < 0) sdk = c.name
}

if (sdk == null) throw new GradleException("No feasible SDK found")
def osxSDK = new File(osxSDKRoot, sdk)

model {
    platforms {
        osx {
            operatingSystem "osx"
        }
    }

    buildTypes {
        release
    }
}

executables {
    main {
        baseName "JavaAppLauncher"
    }

    all {
        binaries.all {
            if (targetPlatform.operatingSystem.macOsX) {
                objcCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                objcCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/darwin"
                objcCompiler.args '-F', "${org.gradle.internal.jvm.Jvm.current().javaHome}/../../"
                linker.args '-framework', "Cocoa"
                linker.args '-isysroot', osxSDK.absolutePath
            }
        }
    }
}

plugindev {
    pluginId 'com.github.cr0.macappbundle'
    pluginName 'gradle-macappbundle-plugin'
    pluginImplementationClass 'com.github.cr0.gradle.macAppBundle.MacAppBundlePlugin'
    pluginDescription 'A Gradle Plugin to create a Mac OSX .app application based on the project with customizable class paths.'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'osx', 'appbundle'
    authorId 'cr0'
    authorName 'Christian Roth'
    authorEmail 'christian.roth@port17.de'
    projectUrl 'https://github.com/cr0/gradle-macappbundle-plugin'
    projectInceptionYear '2015'
    done() // do not omit this
}

bintray {
    user = System.properties['bintray_user']
    key = System.properties['bintray_api_key']
    pkg.repo = 'gradle-plugins'
}

task copyStub(type: Copy, dependsOn: 'mainExecutable') {
    from 'build/binaries/mainExecutable/'
    into 'src/main/resources/com/github/cr0/macAppBundle/'
    outputs.upToDateWhen { false }
}

compileGroovy.dependsOn += copyStub


