import org.gradle.internal.jvm.Jvm

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath 'org.codehaus.groovy:groovy-all:2.3.6'
        classpath 'nu.studer:gradle-plugindev-plugin:1.0.3'
    }
}

apply plugin: 'cpp'
apply plugin: 'objective-c'
apply plugin: 'groovy'
apply plugin: 'nu.studer.plugindev'

group = 'com.github.cr0'

// find osx sdk

String findMacOSSDKRoot() {
    def stdoutBuffer = new ByteArrayOutputStream()
    exec {
        commandLine "xcrun", "--sdk", "macosx", "--show-sdk-path"
        standardOutput = stdoutBuffer
    }
    return stdoutBuffer.toString().trim()
}

def osxSDK = findMacOSSDKRoot()
if (osxSDK == "") throw new GradleException("No feasible SDK found")

model {
    platforms {
        osx {
            operatingSystem "osx"
        }
    }

    buildTypes {
        release
    }

    repositories {
        libs(PrebuiltLibraries) {
            javaHeaders {
                headers.srcDirs "${Jvm.current().javaHome}/include", "${Jvm.current().javaHome}/include/darwin"
            }
        }
    }

    components {
        JavaAppLauncher(NativeExecutableSpec) {
            sources {
                objc {
                    lib library: 'javaHeaders', linkage: 'api'
                    source {
                        srcDirs "src/main/objc"
                        include "**/*.m"
                    }
                }
            }

        }
    }

    binaries {
        all {
            if (toolChain in Clang) {
                linker.args '-framework', "Cocoa"
            }
        }
    }
}

plugindev {
    pluginId 'com.github.cr0.macappbundle'
    pluginName 'gradle-macappbundle-plugin'
    pluginImplementationClass 'com.github.cr0.gradle.macAppBundle.MacAppBundlePlugin'
    pluginDescription 'A Gradle Plugin to create a Mac OSX .app application based on the project with customizable class paths.'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'osx', 'appbundle'
    authorId 'cr0'
    authorName 'Christian Roth'
    authorEmail 'christian.roth@port17.de'
    projectUrl 'https://github.com/cr0/gradle-macappbundle-plugin'
    projectInceptionYear '2015'
    done() // do not omit this
}

bintray {
    user = System.properties['bintray_user']
    key = System.properties['bintray_api_key']
    pkg.repo = 'gradle-plugins'
}

task copyStub(type: Copy, dependsOn: 'JavaAppLauncherExecutable') {
    from 'build/exe/javaAppLauncher/'
    into 'src/main/resources/com/github/cr0/macAppBundle/'
    outputs.upToDateWhen { false }
}

compileGroovy.dependsOn += copyStub


